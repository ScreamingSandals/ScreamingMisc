defaultTasks 'clean', 'screamCompile'

allprojects {
    group = 'org.screamingsandals.misc'
    version = '1.0.0'
}

buildscript {
    repositories {
        jcenter()
        maven {
            name 'screamingsandals'
            url = 'https://repo.screamingsandals.org/public/'
        }
    }
    dependencies {
        classpath 'org.screamingsandals.gradle.builder:screaming-plugin-builder:1.0.29'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.screamingsandals.gradle.builder'

    dependencies {
        compileOnly paper()
        compileOnly waterfall()
    }

    sourceCompatibility = '1.8'

    shadowJar {
        relocate project.group, project.group + '.' + project.name
    }

    enableSpigradleSpigot()

    spigot {
        main = project.group + '.' + project.name + '.MiscBukkitPlugin'
        name = 'ScreamingLibrary_' + project.name
        authors = ['ScreamingSandals']
        apiVersion = '1.13'
        load = STARTUP
    }

    enableSpigradleBungee()

    bungee {
        main = project.group + '.' + project.name + '.MiscBungeePlugin'
        name = 'ScreamingLibrary_' + project.name
        author = 'ScreamingSandals'
    }
}

project(':groovy') {
    version = '3.0.7'
    dependencies {
        shade 'org.codehaus.groovy:groovy:' + version
    }
}

project(':univocity') {
    version = '2.8.3'
    dependencies {
        shade 'com.univocity:univocity-parsers:' + version
    }
}

project(':lettuce-core') {
    version = '5.2.2.RELEASE'
    dependencies {
        shade 'io.lettuce:lettuce-core:' + version
    }
}

project(':kotlin') {
    version = '1.4.21'
    dependencies {
        shade 'org.jetbrains.kotlin:kotlin-stdlib:' + version
    }
}